pluginManagement {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
    }

    plugins {
        id("com.android.application") version "8.10.1"
        id("com.android.library") version "8.10.1"
        id("org.jetbrains.kotlin.android") version '2.2.0'
        id("org.jetbrains.kotlin.plugin.compose") version "2.1.21"
        id("dev.rikka.tools.autoresconfig") version "1.2.2"
        id("dev.rikka.tools.materialthemebuilder") version "1.5.1"
        id("dev.rikka.tools.refine") version "4.4.0"
    }
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)

    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven {
            url("https://jitpack.io")
        }
    }

    versionCatalogs {
        serverLibs {
            from(files('server/server.versions.toml'))
        }

        managerLibs {
            from(files('manager/manager.versions.toml'))
        }
    }
}

if (System.getenv("JITPACK") != "true") {
    include ':server'
    include ':manager'
}
def root = "api"

def propFile = file('local.properties')
def props = new Properties()

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null) {
        if (props["api.useLocal"].equals("true")) {
            root = props["api.dir"]
        }
    }
}

include ':shizuku-aidl'
project(':shizuku-aidl').projectDir = file('shizuku/aidl')

include ':shizuku-api'
project(':shizuku-api').projectDir = file('shizuku/api')